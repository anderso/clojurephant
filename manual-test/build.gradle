plugins {
  id 'dev.clojurephant.clojure'
  id 'dev.clojurephant.clojurescript'
}

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
}

dependencies {
  // clojure
  implementation 'org.clojure:clojure:1.10.3'
  implementation 'org.clojure:clojurescript:1.11.4'

  // testing
  testRuntimeOnly 'org.ajoberstar:jovial:0.3.0'

  // frontend
  implementation 'org.omcljs:om:1.0.0-beta4'
  devImplementation 'devcards:devcards:0.2.6'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

clojureRepl {
  port = 7888
}

clojure {
  builds {
    main {
      aotAll()
      reflection = 'warn'
    }
  }
}

clojurescript {
  builds {
    all {
      compiler {
        outputDir = "public/js/${name}-out"
        assetPath = "js/${name}-out"
      }
    }
    main {
      compiler {
        outputTo = 'public/js/main.js'
        sourceMap = 'public/js/main.js.map'
        optimizations = 'advanced'
        main = 'sample.main'
      }
    }
    dev {
      compiler {
        outputTo = 'public/js/main.js'
        main = 'sample.main'
        preloads = ['sample.dev', 'devtools.preload']
      }
    }
    cards {
      sourceSet.set(sourceSets.dev)
      compiler {
        preloads = ['sample.dev', 'devtools.preload']
        outputTo = 'public/js/cards.js'
        main = 'sample.cards'
      }
    }
    mods {
      sourceSet.set(sourceSets.dev)
      compiler {
        preloads = ['sample.dev', 'devtools.preload']
        modules.create('main') {
          outputTo = 'public/js/main.js'
          entries = ['sample.main']
          dependsOn = ['cljs-base']
        }
        modules.create('cards') {
          outputTo = 'public/js/cards.js'
          entries = ['sample.cards']
          dependsOn = ['cljs-base']
        }
      }
    }
  }
}

// accept build scan terms
if (hasProperty('buildScan')) {
  buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
  }
}
