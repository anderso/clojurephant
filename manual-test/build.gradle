plugins {
  id 'dev.clojurephant.clojure'
  id 'dev.clojurephant.clojurescript'
  id 'eclipse'
  id 'idea'
}

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
}

dependencies {
  // clojure
  implementation 'org.clojure:clojure:1.11.1'
  implementation 'org.clojure:clojurescript:1.11.60'

  // testing
  testRuntimeOnly 'org.ajoberstar:jovial:0.3.0'

  // backend
  implementation 'io.pedestal:pedestal.service:0.5.10'
  implementation 'io.pedestal:pedestal.jetty:0.5.10'

  // frontend
  implementation 're-frame:re-frame:1.2.0'

  // component
  implementation 'com.stuartsierra:component:1.1.0'
  devImplementation 'com.stuartsierra:component.repl:0.2.0'

  // figwheel
  devImplementation 'com.bhauman:figwheel-repl:0.2.18'
  devImplementation 'ring:ring-jetty-adapter:1.9.5'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

clojure {
  builds {
    main {
      aotAll()
      reflection = 'warn'
    }
  }
}

clojurescript {
  builds {
    all {
      compiler {
        outputTo = 'public/js/main.js'
        outputDir = "public/js/out"
        assetPath = "/js/out"
        main = 'sample.main'
      }
    }
    main {
      compiler {
        optimizations = 'advanced'
        sourceMap = 'public/js/main.js.map'
      }
    }
    dev {
      compiler {
        optimizations = 'none'
        sourceMap = true
        preloads = ['sample.dev']
      }
    }
  }
}
