plugins {
  id 'dev.clojurephant.clojure'
  id 'dev.clojurephant.clojurescript'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
  // TODO package tooling API into the clojurephant tooling JAR
  maven {
    name = 'gradle-libs'
    url = 'https://repo.gradle.org/gradle/libs-releases'
    content {
      includeGroup 'org.gradle'
    }
  }
}

dependencies {
  // clojure
  implementation 'org.clojure:clojure:1.11.1'
  implementation 'org.clojure:clojurescript:1.11.60'

  // testing
  testRuntimeOnly 'org.ajoberstar:jovial:0.3.0'

  // backend
  implementation 'io.pedestal:pedestal.service:0.5.10'
  implementation 'io.pedestal:pedestal.jetty:0.5.10'

  // frontend
  implementation 're-frame:re-frame:1.2.0'

  // component
  implementation 'com.stuartsierra:component:1.1.0'
  devImplementation 'com.stuartsierra:component.repl:0.2.0'

  // tooling
  devImplementation 'dev.clojurephant:clojurephant-tooling:0.1.0'
  devImplementation 'cider:piggieback:0.5.3'

  // figwheel
  devImplementation 'com.bhauman:figwheel-repl:0.2.18'
  devImplementation 'com.bhauman:figwheel-main:0.2.18'
  devImplementation 'ring:ring-jetty-adapter:1.9.5'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

clojure {
  builds {
    main {
      aotNamespaces.add('sample.core')
      reflection = 'warn'
    }
  }
}

clojurescript {
  builds {
    all {
      compiler {
        outputTo = 'public/js/main.js'
        outputDir = 'public/js/out'
        assetPath = '/js/out'
        main = 'sample.main'
      }
    }
    main {
      compiler {
        optimizations = 'advanced'
        sourceMap = 'public/js/main.js.map'
      }
    }
    dev {
      compiler {
        optimizations = 'none'
        sourceMap = true
        // preloads = ['sample.dev']
      }
    }
  }
}

tasks.named('clojureRepl') {
  middleware.add('cider.piggieback/wrap-cljs-repl')
}
